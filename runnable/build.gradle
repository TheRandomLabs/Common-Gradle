import org.apache.tools.ant.filters.ReplaceTokens

apply from: "https://raw.githubusercontent.com/TheRandomLabs/Common-Gradle/master/build.gradle"

dependencies {
	implementation "org.slf4j:slf4j-api:${project.slf4jVersion}"
	implementation "org.apache.logging.log4j:log4j-core:${project.log4jVersion}"
	implementation "org.apache.logging.log4j:log4j-slf4j18-impl:${project.log4jVersion}"
}

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property "version", version
    filter(ReplaceTokens, tokens: [VERSION: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

jar {
	manifest {
		attributes "Main-Class": project.mainClass
	}

	from {
		configurations.runtimeClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}
